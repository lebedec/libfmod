WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{
    ("/*" ~ (!"*/" ~ ANY)* ~ "*/")
    | ("//" ~ (!"\n" ~ ANY)*)
}

name = @{ ("_" | ASCII_ALPHANUMERIC)+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

define = _{ "#define" ~ "_FMOD_DSP_H" }
ifndef = _{ "#ifndef" ~ "_FMOD_DSP_H" }
endif = _{ "#endif" }
include = _{ "#include \"fmod_dsp_effects.h\"" }
Directive = { define | ifndef | endif | include }

FundamentalType = {
    "char" |
    "unsigned char" |
    "signed char" |
    "int" |
    "unsigned int" |
    "short" |
    "unsigned short" |
    "long long" |
    "long" |
    "unsigned long long" |
    "unsigned long" |
    "void" |
    "float"
}
UserType = @{name}

base_type = { FundamentalType | UserType }
TypeAlias = { "typedef" ~ base_type ~ name ~ ";" }

OpaqueType = { "typedef" ~ "struct" ~ name ~ name ~ ";"}

Calculation = { "(" ~ (!")" ~ ANY)* ~ ")" }
Literal = { ("-" | "_" | ASCII_ALPHANUMERIC)+ }
value = @{Calculation | Literal }
Constant = { "#define" ~ name ~ value }

Flag = { "#define" ~ name ~ value }
flags = { Flag+ }
flags_type = { FundamentalType }
Flags = { "typedef" ~ flags_type ~ name ~ ";" ~ flags  }

Enumerator = { ","? ~ name ~ ("=" ~ value)? }
enumerators = { Enumerator* }
Enumeration = { "typedef" ~ "enum" ~ name? ~ "{" ~ enumerators ~ ","? ~ "}" ~  name ~ ";" }

as_const = { "const" }
as_array = { "[" ~ ("_" | ASCII_ALPHANUMERIC)+ ~ "]" }
NormalPointer = { "*" }
DoublePointer = { "**" | ("*" ~ "const" ~ "*") }
pointer = { DoublePointer | NormalPointer }
field_type = { FundamentalType | UserType }
Field = { as_const? ~ field_type ~ pointer? ~ name ~ as_array? ~ ";" }
fields = { Field* }
union = { "union" ~ "{" ~ fields ~ "}" ~ ";" }
Structure = { "typedef"? ~ "struct" ~ name ~ "{" ~ fields ~ union? ~ "}" ~ name? ~ ";" }

argument_type = { FundamentalType | UserType }
Argument = { as_const? ~ argument_type ~ pointer? ~ name}
arguments = { (","? ~ Argument)* }
return_type = { FundamentalType | UserType}
varargs = { "," ~ "..." }
Callback = { "typedef" ~ return_type ~ pointer? ~ ("(F_CALLBACK *" | "(F_CALL *") ~ name ~ ")" ~ "(" ~ arguments ~ varargs?  ~ ")" ~  ";" }

Macros = {"#define" ~ name ~ "(" ~ (!"#" ~ ANY)* }

declaration = _{
    OpaqueType |
    Macros |
    Directive |
    Flags |
    TypeAlias |
    Constant |
    Enumeration |
    Structure |
    Callback
}

api = { SOI ~ declaration* ~ EOI  }